apply plugin: 'application'
apply plugin: 'maven'

dependencies {
    def groovyVersion = '2.1.3'

    groovy "org.codehaus.groovy:groovy-all:$groovyVersion"

    compile "org.codehaus.groovy:groovy-all:$groovyVersion",
            'org.apache.ant:ant:1.8.4',     // required for AntBuilder
            'commons-cli:commons-cli:1.2'   // required for CliBuilder

    testCompile 'junit:junit:4.10'
}

mainClassName = "glide.runner.GlideCLI"

evaluationDependsOn(':gae-base-web')

/**
 * run provides a shortcut to run the app  without needing to install glide
 *
 * gradle run -Papp=../samples/blog -Pout=../samples/out
 *
 * run the default sample app if no app is provided
 */
run {
    dependsOn ":gae-base-web:war"

    def argsList = []

    argsList << ["-a", getPathFromProperty("app", "${project.rootProject.projectDir.path}/samples/simple_app")]
    argsList << ["-t", project(':gae-base-web').ext.explodedWarDir.path]
    argsList << ["-o", getPathFromProperty("out", "${project.rootProject.projectDir.path}/install/generated/sample")]

    if (project.hasProperty("gae")){ argsList << ["-g", project.getProperty("gae")] }
    if (project.hasProperty("port")){ argsList << ["-p", project.getProperty("port")] }
    if (project.hasProperty("help")){ argsList << "-h" }
    if (project.hasProperty("quiet")){ argsList << "-q" }

    args argsList.flatten()
}


applicationDistribution.from(project(':gae-base-web').explodedWarDir) {
    into "template"
}

installApp {
    dependsOn ":runner:build", ":gae-base-web:war"

    def install_location = getPathFromProperty("dir", project.rootProject.projectDir.path + "/install")
    into install_location

    doLast {
        println "installing to $install_location"
    }
}

distZip {
    dependsOn ":runner:build", ":gae-base-web:war"
}

startScripts {
    applicationName = "glide"       // this will be the name of script that is generated

    // TODO to check if this works on windows
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile    = file getUnixScript()
        //  windowsScriptFile.text = windowsScriptFile.text.replace('%APP_HOME%\lib\config', '%APP_HOME%\config')
        unixScriptFile.text    = unixScriptFile.text.replace('APP_HOME="`pwd -P`"', 'APP_HOME="`pwd -P`"\nexport GLIDE_HOME=$APP_HOME')
        if (!unixScriptFile.text.contains("export GLIDE_HOME")) {
            println "GLIDE_HOME is not set. This may lead to improper installation"
        }
    }
}

processResources {
    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
            build_at: new Date().dateTimeString,
            version: project.version
    ]
}

/**
 * extracts path from system properties and if no path given, uses default value
 *
 * use absolute path or path relative to current working dir
 *
 * WARNING: gradle installTo -Pdir=~/.glide may or may not work.
 *
 * @returns a file object
 */
def getPathFromProperty(prop_name, default_value){
    if (project.hasProperty(prop_name) ) {
        String dir = project.property(prop_name)

        String user_home = System.getProperty("user.home");
        if (dir.startsWith("~") && file(user_home).isDirectory())
            dir = dir.replaceFirst("~",user_home)

        return file(dir)
    }
    return file(default_value)
}
