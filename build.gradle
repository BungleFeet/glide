allprojects {
    apply plugin: 'groovy'
    apply plugin: 'idea'

    group 'glide'
    version '0.2-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
        // enable for Gaelyk Snapshots
        // maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }

    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

ext {
    groovyVersion = '2.1.3'
    gaeVersion = '1.8.0'
    gaelykVersion = '2.0'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.5'
    jarFile = file("${project.projectDir}/tools/gradle-wrapper.jar")
    scriptFile = file("${project.projectDir}/gradlew")
}

task installGae << {
    println "Downloading Google App Engine, it may take upto several minutes depending on the internet speed."

    def downloadHost = "http://googleappengine.googlecode.com/files"
    def archiveName = "appengine-java-sdk-${gaeVersion}.zip"

    def localArchiveDir = "install/archive"
    def extractToDir = 'install/gae/'

    // download the sdk
    ant.mkdir(dir: localArchiveDir) // ant `get` fails if `dir` does not exist
    ant.get(src: "${downloadHost}/${archiveName}", dest: "${localArchiveDir}/${archiveName}", skipexisting: true)
    // TODO verbose ant log poduces to many dots (lines) and it may kill imp user info from terminal, change to something more intelligent

    // unzip it
    ant.mkdir(dir: extractToDir)
    ant.unzip(src: "${localArchiveDir}/${archiveName}", dest: extractToDir, overwrite: false)
}
